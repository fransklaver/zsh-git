# wunjo prompt theme

autoload -U zgitinit
zgitinit

prompt_zsh-git_help () {
  cat <<'EOF'

  prompt zsh-git

EOF
}

revstring() {
	git rev-parse --short $1 2>/dev/null
}

coloratom() {
	local off=$1 atom=$2
	if [[ $atom[1] == [[:upper:]] ]]; then
		off=$(( $off + 60 ))
	fi
	echo $(( $off + $colorcode[${(L)atom}] ))
}
colorword() {
	local fg=$1 bg=$2 att=$3
	local -a s

	if [ -n "$fg" ]; then
		s+=$(coloratom 30 $fg)
	fi
	if [ -n "$bg" ]; then
		s+=$(coloratom 40 $bg)
	fi
	if [ -n "$att" ]; then
		s+=$attcode[$att]
	fi

	echo "%{"$'\e['${(j:;:)s}m"%}"
}

prompt_zsh-git_setup() {
	local verbose
	if [[ $TERM == screen* ]] && [ -n "$STY" ]; then
		verbose=
	else
		verbose=1
	fi

	typeset -A colorcode
	colorcode[black]=0
	colorcode[red]=1
	colorcode[green]=2
	colorcode[yellow]=3
	colorcode[blue]=4
	colorcode[magenta]=5
	colorcode[cyan]=6
	colorcode[white]=7
	colorcode[default]=9
	colorcode[k]=$colorcode[black]
	colorcode[r]=$colorcode[red]
	colorcode[g]=$colorcode[green]
	colorcode[y]=$colorcode[yellow]
	colorcode[b]=$colorcode[blue]
	colorcode[m]=$colorcode[magenta]
	colorcode[c]=$colorcode[cyan]
	colorcode[w]=$colorcode[white]
	colorcode[.]=$colorcode[default]

	typeset -A attcode
	attcode[none]=00
	attcode[bold]=01
	attcode[faint]=02
	attcode[standout]=03
	attcode[underline]=04
	attcode[blink]=05
	attcode[reverse]=07
	attcode[conceal]=08
	attcode[normal]=22
	attcode[no-standout]=23
	attcode[no-underline]=24
	attcode[no-blink]=25
	attcode[no-reverse]=27
	attcode[no-conceal]=28

	local -A pc
	pc[default]='default'
	pc[date]='cyan'
	pc[time]='Blue'
	pc[host]='Green'
	pc[user]='cyan'
	pc[punc]='yellow'
	pc[line]='magenta'
	pc[hist]='green'
	pc[path]='Cyan'
	pc[shortpath]='default'
	pc[rc]='red'
	pc[scm_branch]='Cyan'
	pc[scm_commitid]='Yellow'
	pc[scm_status_dirty]='Red'
	pc[scm_status_staged]='Green'
	pc[#]='Yellow'
	for cn in ${(k)pc}; do
		pc[${cn}]=$(colorword $pc[$cn])
	done
	pc[reset]=$(colorword . . 00)

	typeset -Ag wunjo_prompt_colors
	wunjo_prompt_colors=(${(kv)pc})

	local p_date p_line p_rc

	p_date="$pc[time]%D{%T}$pc[reset]"

	p_line="$pc[line]%y$pc[reset]"

	PROMPT="\$(prompt_zsh-git_setinfo)"
	RPROMPT=$p_date

	prompt_gentoo_prompt=${1:-'blue'}
	prompt_gentoo_user=${2:-'green'}
	prompt_gentoo_root=${3:-'red'}

	if [ "$USER" = 'root' ]
	then
		base_prompt="%B%F{$prompt_gentoo_root}%m "
	else
		base_prompt="%B%F{$prompt_gentoo_user}%n@%m "
	fi
	post_prompt="%b%f"

	#setopt noxtrace localoptions

	path_prompt="%B%F{$prompt_gentoo_prompt}%1~"


	PROMPT+="$base_prompt$path_prompt %# $post_prompt"
	export PROMPT
	precmd_functions+='prompt_wunjo_precmd'
}

prompt_zsh-git_setinfo() {
	if zgit_isgit; then
		prompt_info=
		prompt_info+="$(prompt_wunjo_scm_branch)"
		prompt_info+=" $(prompt_wunjo_scm_status)"
		#prompt_info+="%(?.. $pc[rc]exited %1v$pc[reset])"
		#Rprompt_info+="\$(prompt_wunjo_scm_status)"
		echo -e $prompt_info
    echo -en "\0" 
	fi
}

prompt_wunjo_precmd() {
	local ex=$?
	psvar=()

	if [[ $ex -ge 128 ]]; then
		sig=$signals[$ex-127]
		psvar[1]="sig${(L)sig}"
	else
		psvar[1]="$ex"
	fi
}

prompt_display_name() {
	echo "$pc[scm_branch]$1$pc[punc]"
}

prompt_display_commit() {
	echo "($pc[scm_commitid]$1$pc[punc])"
}

prompt_string() {
	name="$1"
	ref=$2
	echo "$(prompt_display_name $name)$(prompt_display_commit $ref)$pc[reset]"
}

prompt_construct() {
	item=$1
	commit=$(revstring $zgit_info[$item])
	name=$(git name-rev --name-only $zgit_info[$item])

	prompt_string $name $commit
}

prompt_wunjo_scm_branch() {
	zgit_isgit || return
	local -A pc
	pc=(${(kv)wunjo_prompt_colors})

	head=$(zgit_head)
	gitcommit=$(revstring $head)

	local -a commits

	if zgit_rebaseinfo; then
		orig=$(prompt_construct rb_head)
		onto=$(prompt_construct rb_onto)

		if [ -n "$zgit_info[rb_upstream]" ] && [ $zgit_info[rb_upstream] != $zgit_info[rb_onto] ]; then
			upstream=$(prompt_construct rb_upstream)
			commits+="rebasing $upstream..$orig onto $onto"
		else
			commits+="rebasing $onto..$orig"
		fi

		local -a revs
		revs=($(git rev-list $zgit_info[rb_onto]..HEAD))
		if [ $#revs -gt 0 ]; then
			commits+="\n$#revs commits in"
		fi

		if [ -f $zgit_info[dotest]/message ]; then
			mess=$(head -n1 $zgit_info[dotest]/message)
			commits+="$mess"
		fi
	elif [ -n "$gitcommit" ]; then
		commits+=$(prompt_string $head $gitcommit)
		local track_merge=$(zgit_tracking_merge)
		if [ -n "$track_merge" ]; then
			if git rev-parse --verify -q $track_merge >/dev/null; then
				local track_remote=$(zgit_tracking_remote)
				local tracked=$(revstring $track_merge 2>/dev/null)

				local -a revs
				revs=($(git rev-list --reverse $track_merge..HEAD))
				if [ $#revs -gt 0 ]; then
					local base=$(revstring $revs[1]~1)
					local base_name=$(git name-rev --name-only $base)
					local word_commits='commit'
					if [ $#revs -gt 1 ]; then
						word_commits+='s'
					fi

					local conj="since"
					if [[ "$base" == "$tracked" ]]; then
						conj+=" tracked"
						tracked=
					fi
					commits+="$#revs $word_commits $conj "
					commits+=$(prompt_string $base_name $base)
				fi

				if [ -n "$tracked" ]; then
					local track_name=$track_merge
					if [[ $track_remote == "." ]]; then
						track_name=${track_name##*/}
					fi
					tracked=$(revstring $tracked)
					commits+="tracking $(prompt_display_name $track_name)"
					if [[ "$tracked" != "$gitcommit" ]]; then
						commits[$#commits]+=$(prompt_display_commit $tracked)
					fi
					commits[$#commits]+="$pc[reset]"
				fi
			fi
		fi
	fi

	gitsvn=$(git rev-parse --verify -q --short git-svn)
	if [ $? -eq 0 ]; then
		gitsvnrev=$(zgit_svnhead $gitsvn)
		gitsvn=$(revstring $gitsvn)
		if [ -n "$gitsvnrev" ]; then
			local svninfo=''
			local -a revs
			svninfo+="$pc[default]svn$pc[punc]:$(prompt_display_name r$gitsvnrev)"
			revs=($(git rev-list git-svn..HEAD))
			if [ $#revs -gt 0 ]; then
				svninfo+="$pc[punc]@$pc[default]HEAD~$#revs"
				svninfo+="$pc[punc]($(prompt_display_commit $gitsvn)$pc[reset])"
			fi
			commits+=$svninfo
		fi
	fi

	if [ $#commits -gt 0 ]; then
		echo -n "${(j: :)commits} "
	fi
}

prompt_wunjo_scm_status() {
	zgit_isgit || return
	local -A pc
	pc=(${(kv)wunjo_prompt_colors})

	if zgit_inworktree; then
		if ! zgit_isindexclean; then
			echo -n "$pc[scm_status_staged][uncommited]"
		fi

		local -a dirty
		if ! zgit_isworktreeclean; then
			dirty+='%B[not added]%b'
		fi

		if zgit_hasunmerged; then
			dirty+='[unmerged]'
		fi

		if zgit_hasuntracked; then
			dirty+='[untracked]'
		fi

		if [ $#dirty -gt 0 ]; then
			echo -n "$pc[scm_status_dirty]${(j::)dirty}"
		fi
	fi
	
	echo $pc[reset]
}

prompt_zsh-git_setup "$@"

# vim:set ft=zsh:
